plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id "com.diffplug.spotless"
    id 'com.google.dagger.hilt.android'
    id 'com.google.devtools.ksp'
}

spotless {
    kotlin {
        target '**/*.kt'
        ktlint()
            .editorConfigOverride([
                'ktlint_function_naming_ignore_when_annotated_with': 'Composable',
            ])
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}

android {
    namespace 'seamuslowry.daytracker'
    compileSdk 34

    defaultConfig {
        applicationId "seamuslowry.daytracker"
        minSdk 33
        targetSdk 34
        versionCode 36
        versionName "0.30.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    def bom = platform("androidx.compose:compose-bom:$compose_bom_version")
    implementation bom

    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation 'androidx.compose.material:material-icons-extended'
    implementation "androidx.compose.material3:material3"
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.4'
    implementation 'androidx.activity:activity-compose:1.9.1'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.2.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "io.github.fornewid:placeholder-material3:2.0.0"
    implementation "androidx.datastore:datastore-preferences:1.1.1"

    ksp "androidx.room:room-compiler:$room_version"
    ksp "com.google.dagger:hilt-compiler:$hilt_version"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation bom
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"

    debugImplementation bom
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
}

tasks.register('printVersionName') {
    doLast {
        println android.defaultConfig.versionName
    }
}

tasks.register('printVersionCode') {
    doLast {
        println android.defaultConfig.versionCode
    }
}